% Basic Sorts

fof(disjoint_sorts, axiom,
    ![X]: (
        ~(entity(X) & value(X)) &
        ~(puroterm(X) & entity(X)) &
        ~(puroterm(X) & value(X)) &
        ~(role(X) & entity(X)) &
        ~(role(X) & value(X)) &
        ~(role(X) & puroterm(X))
    )).


% Fundamental Relations

fof(associatedwith_sorts, axiom,
    ![X, T]: (
        associatedwith(X, T) =>
        (entity(X) & puroterm(T))
    )).

% Instantiation Relation
fof(instanceof_sorts, axiom,
    ![X, Y]: (
        instanceof(X, Y) =>
        (entity(X) & entity(Y))
    )).


% Auxiliary PURO terms disjointness predicate

fof(disjointTerms_definition, axiom,
   ![T_1, T_2]: ( disjointTerms(T_1, T_2) <=>
                  ![X]: ~(associatedwith(X, T_1) & associatedwith(X, T_2) )
   )
).


% Particulars and Universals

fof(particular_universal_puro_term, axiom,
    puroterm(particular)).

fof(particular_universal_puro_term, axiom,
    puroterm(universal)).

fof(disjoint_particular_universal, axiom,
    disjointTerms(particular, universal)).

fof(exists_instanceof_universal, axiom,
    ![Y]: (
        ? [X]: instanceof(X, Y) =>
        associatedwith(Y, universal)
    )).


% Particulars

fof(btype_brelation_battribute_terms, axiom,
    puroterm(bobject)).

fof(btype_brelation_battribute_terms, axiom,
    puroterm(brelationship)).

fof(btype_brelation_battribute_terms, axiom,
    puroterm(bvaluation)).

fof(particular_object_relationship_valuation, axiom,
    ![X]: (
        associatedwith(X, particular) <=>
        (associatedwith(X, bobject) | associatedwith(X, brelationship) | associatedwith(X, bvaluation))
    )).

fof(disjoint_bobject_brelationship_bvaluation, axiom,
    disjointTerms(bobject, brelationship) &
    disjointTerms(bobject, bvaluation) &
    disjointTerms(brelationship, bvaluation)
).


% Universals - Types, Relations, Attributes

fof(btype_brelation_battribute_terms, axiom,
    puroterm(btype)).

fof(btype_brelation_battribute_terms, axiom,
    puroterm(brelation)).

fof(btype_brelation_battribute_terms, axiom,
    puroterm(battribute)).

fof(universal_type_relation_attribute, axiom,
    ![X]: (
        associatedwith(X, universal) <=>
        (associatedwith(X, btype) | associatedwith(X, brelation) | associatedwith(X, battribute))
    )).

fof(disjoint_btype_brelation_battribute, axiom,
    ![X]: (
        disjointTerms (btype, brelation) &
        disjointTerms(btype, battribute) &
        disjointTerms(brelation, battribute)
    )
).


% Hierarchy of Types

fof(btype_brelation_battribute_terms, axiom,
    puroterm(btype_1)).

fof(btype_brelation_battribute_terms, axiom,
    puroterm(btype_2)).

fof(btype_brelation_battribute_terms, axiom,
    puroterm(btype_3)).

fof(btype_hierarchy_terms, axiom,
    ![X]: (
        (associatedwith(X, btype_1) => associatedwith(X, btype)) &
        (associatedwith(X, btype_2) => associatedwith(X, btype)) &
        (associatedwith(X, btype_3) => associatedwith(X, btype))
    )).

fof(disjoint_btype_levels, axiom,
        disjointTerms(btype_1, btype_2) &
        disjointTerms(btype_1, btype_3) &
        disjointTerms(btype_2, btype_3)
).

fof(instance_btype_relationship, axiom,
    ![X, Y]: (
        (instanceof(X, Y) & associatedwith(Y, btype_1)) =>
        associatedwith(X, bobject)
    )).

fof(instance_btype_inheritance, axiom,
    ![X, Y]: (
        (instanceof(X, Y) & associatedwith(Y, btype_2)) =>
        associatedwith(X, btype_1)
    )).

fof(instance_btype_inheritance, axiom,
    ![X, Y]: (
        (instanceof(X, Y) & associatedwith(Y, btype_3)) =>
        associatedwith(X, btype_2)
    )).

fof(instance_object_type_relation, axiom,
    ![X, Y]: (
        (instanceof(X, Y) & associatedwith(X, bobject)) =>
        (associatedwith(Y, btype_1) | (associatedwith(Y, btype) & heterogeneous(Y)))
    )).

fof(instance_type_hierarchy_relation, axiom,
    ![X, Y]: (
        (instanceof(X, Y) & associatedwith(X, btype_1)) =>
        (associatedwith(Y, btype_2) | (associatedwith(Y, btype) & heterogeneous(Y)))
    )).

fof(instance_type_hierarchy_relation, axiom,
    ![X, Y]: (
        (instanceof(X, Y) & associatedwith(X, btype_2)) =>
        (associatedwith(Y, btype_3) | (associatedwith(Y, btype) & heterogeneous(Y)))
    )).

fof(homogeneous_btype_1, axiom,
    ![X]: (associatedwith(X, btype_1) => ~heterogeneous(X))).

fof(homogeneous_btype_2, axiom,
    ![X]: (associatedwith(X, btype_2) => ~heterogeneous(X))).

fof(homogeneous_btype_3, axiom,
    ![X]: (associatedwith(X, btype_3) => ~heterogeneous(X))).


% Relationships

fof(hasParticipant_sorts, axiom,
    ![R, P, X]: (
        hasParticipant(R, P, X)
        => (associatedwith(R, brelationship)
            & entity(X)
            & role(P)
        )
    )
).

fof(brelationship_at_least_two_participants_or_roles, axiom,
    ![R]: (
        associatedwith(R, brelationship)
        => ?[X_1, X_2, R_1, R_2]: (
            (X_1 != X_2 | R_1 != R_2)
            & hasParticipant(R, R_1, X_1)
            & hasParticipant(R, R_2, X_2)
     )
   )
).


% Relations

fof(instanceof_brelation_only_brelationship, axiom,
    ![X, Y]: (
        (instanceof(X, Y) & associatedwith(Y, brelation)) =>
        associatedwith(X, brelationship)
    )
).

fof(brelationship_instanceof_only_brelation_or_heterogen_btype, axiom,
    ![X, Y]: (
        (instanceof(X, Y) & associatedwith(X, brelationship)) =>
        (associatedwith(Y, brelation) | (associatedwith(Y, btype) & heterogeneous(Y)))
    )
).

fof(homogeneous_brelation, axiom,
    ~?[X]: (heterogeneous(X) & associatedwith(X, brelation))
).


% Valuations

fof(hasvaluation_only_entity_and_bvaluation, axiom,
    ![X, V]: (
        hasValuation(X, V) =>
        (entity(X) & associatedwith(V, bvaluation))
    )
).

fof(hasvalue_only_bvaluation_and_value, axiom,
    ![V, W]: (
        hasValue(V, W) =>
        (associatedwith(V, bvaluation) & value(W))
    )
).

fof(bvaluation_hasvaluation_some_unique_entity, axiom,
    ![V]: (
        associatedwith(V, bvaluation) =>
        ?[X_1]: (
            hasValuation(X_1, V) &
            ![X_2]: (hasValuation(X_2, V) => (X_1 = X_2))
        )
    )
).

fof(bvaluation_hasvalue_some_unique_value, axiom,
    ![V]: (
        associatedwith(V, bvaluation) =>
        ?[W_1]: (
            hasValue(V, W_1) &
            ![W_2]: (hasValue(V, W_2) => (W_1 = W_2))
        )
    )
).


% Attributes

fof(instanceof_battribute_only_bvaluation, axiom,
    ![X, Y]: (
        (instanceof(X, Y) & associatedwith(Y, battribute)) =>
        associatedwith(X, bvaluation)
    )
).

fof(bvaluation_instanceof_only_battribute_or_heterogen_btype, axiom,
    ![X, Y]: (
        (instanceof(X, Y) & associatedwith(X, bvaluation)) =>
        (associatedwith(Y, battribute) | (associatedwith(Y, btype) & heterogeneous(Y)))
    )
).

fof(homogeneous_battribute, axiom,
    ~?[X]: (heterogeneous(X) & associatedwith(X, battribute))
).


% Types of Relations and Attributes

fof(puroterm_btyper, axiom,
    puroterm(btype_r)
).
fof(puroterm_btypea, axiom,
    puroterm(btype_a)
).

fof(btyper_or_btypea_implies_btype, axiom,
    ![X]: (
        (associatedwith(X, btype_r) | associatedwith(X, btype_a) =>
        associatedwith(X, btype))
    )
).

fof(disjoint_btypes_a_r_1_2_3, axiom,
    disjointTerms(btype_r, btype_a)
    & disjointTerms(btype_a, btype_1)
    & disjointTerms(btype_a, btype_2)
    & disjointTerms(btype_a, btype_3)
    & disjointTerms(btype_r, btype_1)
    & disjointTerms(btype_r, btype_2)
    & disjointTerms(btype_r, btype_3)
).

fof(instanceof_btyper_only_brelation, axiom,
    ![X, Y]: (
        (instanceof(X, Y) & associatedwith(Y, btype_r)) =>
        associatedwith(X, brelation)
    )
).

fof(brelation_instanceof_only_btyper_or_heterogen_btype, axiom,
    ![X, Y]: (
        (instanceof(X, Y) & associatedwith(X, brelation)) =>
        (associatedwith(Y, btype_r) | (associatedwith(Y, btype) & heterogeneous(Y)))
    )
).

fof(instanceof_btypea_only_battribute, axiom,
    ![X, Y]: (
        (instanceof(X, Y) & associatedwith(Y, btype_a)) =>
        associatedwith(X, battribute)
    )
).

fof(battribute_instanceof_only_btypea_or_heterogen_btype, axiom,
    ![X, Y]: (
        (instanceof(X, Y) & associatedwith(X, battribute)) =>
        (associatedwith(Y, btype_a) | (associatedwith(Y, btype) & heterogeneous(Y)))
    )
).

fof(homogeneous_btype_a, axiom,
    ![X]: (associatedwith(X, btype_a) => ~heterogeneous(X))
).

fof(homogeneous_btype_r, axiom,
    ![X]: (associatedwith(X, btype_r) => ~heterogeneous(X))
).


% Kinds of Relationships: Instantiation, Axiom, Fact

fof(puroterm_binstantiation, axiom,
    puroterm(binstantiation)
).

fof(puroterm_baxiom, axiom,
    puroterm(baxiom)
).

fof(puroterm_bfact, axiom,
    puroterm(bfact)
).

fof(brelationship_equiv_binstantiation_or_baxiom_or_bfact, axiom,
    ![R]: (
        associatedwith(R, brelationship) <=>
        (associatedwith(R, binstantiation) | associatedwith(R, baxiom) | associatedwith(R, bfact))
    )
).

fof(disjoint_binstantiation_baxiom_bfact, axiom,
    disjointTerms(binstantiation, baxiom) &
    disjointTerms(binstantiation, bfact) &
    disjointTerms(baxiom, bfact)
).


% Instantiation

fof(role_axiom, axiom, relrole(inst) & relrole(type) & inst != type).

% Main axiom 
fof(Main_axiom, axiom,
    ![R] : (associatedwith(R, binstantiation) =>
        ?[X, T] : (
            hasParticipant(R, inst, X) &
	    hasParticipant(R, type, T) &
	    X!=T &
            instanceof(X, T) &
            ![Y, P] : (
                hasParticipant(R, P, Y) =>
                ((P = inst & Y = X) |
                 (P = type & Y = T))
            )
        )
    )
).


% Type definition for Binstantiation 
fof(type_definition_binstantiation, axiom,
    ![X, T] :
      ( binstanceof(X,T)
        <=>
        ( ?[R] :
            ( binstantiation(R)
              & hasParticipant(R,inst,X)
              & hasParticipant(R,type,T)
            )
        )
      )
).



% Axioms - Disjointness and Subsumption

fof(baxiom_participants_only_universals, axiom,
    ![R, U, P]: (
        (associatedwith(R, baxiom) & hasParticipant(R, P, U) & role(P)) =>
        associatedwith(U, universal)
    )
).

fof(bdisjointness_puroterm, axiom,
    puroterm(bdisjointness)).

fof(bsubsumption_puroterm, axiom,
    puroterm(bsubsumption)).

fof(disjoint_bdisjunction_bdisjointness, axiom,
    disjointTerms(bsubsumption, bdisjointness)
).

fof(bsubsumption_bdisjointness_are_baxioms, axiom,
    ![R]: (
       associatedwith(R, bsubsumption) | associatedwith(R, bdisjointness) => associatedwith(R, baxiom)
    )
).


% Subsumption

fof(role_subsumption_axiom, axiom,
    relrole(subuniv) & relrole(superuniv) & (subuniv != superuniv)
).
% Subsumption Definition
fof(bsubsumption_axiom, axiom,
  ( bsubsume(U,V)
    <=>
    ( ? [R] :
        ( bsubsumption(R)
        & hasParticipant(R, subuniv, U)
        & hasParticipant(R, superuniv, V) ) ) )
).

fof(bsubsumption_implies_subsumption_no_other_participants, axiom,
    ![R]: (
        associatedwith(R, bsubsumption) =>
        ?[U, V]: (
            hasParticipant(R, subuniv, U) &
            hasParticipant(R, superuniv, V) &
            ![X]: (
                instanceof(X, U) => instanceof(X, V)
            ) &
            ![W, P]: (
                hasParticipant(R, P, W) =>
                ((P = subuniv & W = U) |
                 (P = superuniv & W = V))
            )
        )
    )
).


% Disjunction

fof(role_disjointness_axiom, axiom,
    relrole(disjunct)
).

fof(bdisjointness_participant_only_disjunct, axiom,
    ![R, P, U]: (
        associatedwith(R, bdisjointness) &
        hasParticipant(R, P, U) =>
        P = disjunct
    )
).

fof(bdisjointness_participants_pairwise_disjoint, axiom,
    ![R, P, U, V]: (
        associatedwith(R, bdisjointness) &
        hasParticipant(R, P, U) &
        hasParticipant(R, P, V) &
        U != V =>
        ~?[X]: (
            instanceof(X, U) & instanceof(X, V)
        )
    )
).

